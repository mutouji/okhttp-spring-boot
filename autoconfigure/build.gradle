plugins {
    id "findbugs"
    id "checkstyle"
    id "jacoco"
    id "io.spring.dependency-management" version "1.0.4.RELEASE"
}
apply plugin: "io.freefair.lombok"
apply plugin: "io.freefair.maven-jars"
apply plugin: "io.freefair.javadoc-links"
apply plugin: "io.freefair.maven-publish-java"

publishing.publications.mavenJava.artifactId = "okhttp-spring-boot-autoconfigure"

javadoc.options.linkSource true

checkstyle.toolVersion = "8.8"

compileJava.dependsOn(processResources)

dependencyManagement {
    imports {
        mavenBom 'org.springframework.boot:spring-boot-dependencies:2.0.2.RELEASE'
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

jacocoTestReport.reports.xml.enabled = true

configurations {
    optional
    compileOnly.extendsFrom(optional)
    testCompile.extendsFrom(optional)
}

dependencies {
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    compile 'org.springframework.boot:spring-boot-autoconfigure'
    compile 'org.slf4j:slf4j-api'

    optional 'org.springframework:spring-web'

    optional 'com.squareup.okhttp3:okhttp:3.+'
    optional 'com.squareup.okhttp3:logging-interceptor:3.+'

    optional 'com.squareup.okhttp:okhttp:2.+'
    optional 'com.squareup.okhttp:logging-interceptor:2.+'

    testCompile 'org.springframework.boot:spring-boot-starter-test'
}
